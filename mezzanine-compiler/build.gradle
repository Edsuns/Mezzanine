apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

project.ext {
    publishArtifactId = "${project.baseArtifactId}-compiler"
}

dependencies {
    // rx
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    // nullability
    compile 'com.intellij:annotations:12.0@jar'

    // annotation processing
    compile 'com.google.auto.service:auto-service:1.0-rc3'
    kapt 'com.google.auto.service:auto-service:1.0-rc3'

    // code generation
    compile 'com.squareup:javapoet:1.8.0'
    compile 'org.apache.commons:commons-lang3:3.0'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile project(':mezzanine')

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'com.google.testing.compile:compile-testing:0.8'

}

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

jacoco {
    toolVersion = '0.7.9' // See http://www.eclemma.org/jacoco/
}

// gradlew clean build generateRelease
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

def groupId = project.publishGroupId
def artifactId = project.publishArtifactId
def version = project.versionName

def localReleaseDest = "${buildDir}/release/${version}"

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        pom.packaging = 'jar'

        repository(url: "file://${localReleaseDest}")
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "release-${version}.zip"
}

task generateRelease

generateRelease.doLast {
    println "Release ${version} can be found at ${localReleaseDest}/"
    println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)

artifacts {
    archives sourcesJar
    archives javadocsJar
}